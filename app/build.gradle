apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


// 설정변수
ext {
    appMode = '"dev"'   // 운영 - release , 개발 - dev
    isLogBlock = 'false' // 운영 - true , 개발 - false
}

// 빌드 카운트 기록
def buildCnt = ''

android {
    compileSdkVersion 30


    defaultConfig {
        applicationId "com.ghj.browser"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 10005
        versionName "1.0.5"

        multiDexEnabled true

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        all {
            buildConfigField('String', 'BUILD_CONFIG_APP_MODE', project.ext.appMode)
            buildConfigField('boolean', 'BUILD_CONFIG_IS_LOG_BLOCK', project.ext.isLogBlock)
        }

        release {
            if( project.ext.appMode == '"release"')
            {
                def file = file('version.properties');
                if( file.canRead() )
                {
                    def Properties versionProp = new Properties();
                    versionProp.load( new FileInputStream(file) )
                    def count = versionProp['BUILD_COUNT'].toInteger() + 1
                    buildCnt = '(' + count.toString() + ')'

                    versionProp['BUILD_COUNT'] = count.toString()
                    versionProp.store(file.newWriter() , null)
                }
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def filename = "${outputFileName}".replace("app-release", "browser").replace(".apk", "");
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd')

            outputFileName = "${filename}_v${variant.versionCode}_${formattedDate}${buildCnt}.apk"

            println "outputFileName = " + outputFileName
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'


    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    // Annotation processor
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.3.1"


    // multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    // coordinator layout
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"

    // recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // material design
    implementation 'com.google.android.material:material:1.4.0-alpha02'

    // gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // in app review
    implementation 'com.google.android.play:review:2.0.0'


//    testImplementation 'junit:junit:4.12'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
